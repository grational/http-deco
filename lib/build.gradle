/*
 * This build file was auto generated by 'gigawatt'
 * @author grational
 * @date 17-11-2016 11.14
 */

// [plugins] this block must be the first
plugins {
	// support for groovy
	id 'groovy'
	// support for the api keyword in the dependencies section
	id 'java-library'
	// required by jitpack
	id 'maven-publish'
	// awesome git tag / gradle version integration
	id 'pl.allegro.tech.build.axion-release' version '1.21.0'
}

// [general] configuration
project.ext {
	groovyGroupId = 'org.apache.groovy'
	groovyVersion = '4.0.29'
}

project.with {
	//project.name is set in the settings.gradle with rootProject.name
	group       'it.grational'
	version     scmVersion.version // '0.5.0' (remove -SNAPSHOT for releases)
	description 'Provide all major HTTP method implementations and additional functionalities as caching and retrying.'
}

java {
	// java features available in the source code
	sourceCompatibility = JavaVersion.toVersion('21')
	// java version 21 is required to execute the project
	targetCompatibility = sourceCompatibility
}

repositories {
	mavenCentral()

	maven { url "https://jitpack.io" }
}

dependencies {
	implementation (
		'redis.clients:jedis:7.0.0',
	)

	api (
		"$groovyGroupId:groovy:$groovyVersion",
		"$groovyGroupId:groovy-json:$groovyVersion"
	)

	// We use the awesome Spock testing and specification framework
	testImplementation (
		'org.spockframework:spock-core:2.3-groovy-4.0',
		'com.github.grational:specification-support:release-1.1.3',
		"$groovyGroupId:groovy-dateutil:$groovyVersion"
	)
}

// [tasks] configuration //
tasks.withType(Test) {
	useJUnitPlatform()
	maxParallelForks = Runtime.runtime.availableProcessors() / 2
	jvmArgs('--add-opens=java.base/java.util=ALL-UNNAMED')
}
jar {
	dependsOn 'check'
}

check {
	dependsOn 'clean'
}

// [tasks] definition
task uniTest(type: Test) {
	description = 'unit tests task'
	exclude '**/*ISpec*'
	exclude '**/*FSpec*'
}
task intTest(type: Test) {
	description = 'integration tests task'
	exclude '**/*USpec*'
	exclude '**/*FSpec*'
}
task funTest(type: Test) {
	description = 'functional tests task'
	exclude '**/*USpec*'
	exclude '**/*ISpec*'
}
gradle.startParameter.excludedTaskNames += "uniTest"
gradle.startParameter.excludedTaskNames += "intTest"
gradle.startParameter.excludedTaskNames += "funTest"

// required to work with jitpack.io
publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.github.grational'
			artifactId = 'http-deco'
			version = scmVersion.version // '0.5.0' (remove -SNAPSHOT for releases)
			from components.java
		}
	}
}

// vim: ft=groovy:fdm=indent
